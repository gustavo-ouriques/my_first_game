extends CharacterBody2D

enum EsqueletoState{
	walk,
	attack,
	dead
}

const SPINNING_BONE = preload("uid://dk3xlgjwsk3g4")
@onready var animated: AnimatedSprite2D = $AnimatedSprite2D
@onready var hitbox: Area2D = $Hitbox
@onready var wall_detector: RayCast2D = $WallDetector
@onready var ground_detector: RayCast2D = $GroundDetector
@onready var player_detector: RayCast2D = $PlayerDetector
@onready var bone_start_position: Node2D = $BoneStartPosition

const SPEED = 80.0
const JUMP_VELOCITY = -300.0

var status: EsqueletoState

var direction = 1
var can_throw = true

func _ready() -> void:
	go_to_walk_state()

func _physics_process(delta: float) -> void:
	if not is_on_floor():
		velocity += get_gravity() * delta

	match status:
		EsqueletoState.walk:
			walk_state(delta)
		EsqueletoState.attack:
			attack_state(delta)
		EsqueletoState.dead:
			dead_state(delta)
	
	move_and_slide()

func go_to_walk_state():
	status = EsqueletoState.walk
	animated.play("walk")
	
func go_to_attack_state():
	status = EsqueletoState.attack
	animated.play("attack")
	velocity = Vector2.ZERO
	can_throw = true
	
func go_to_dead_state():
	status = EsqueletoState.dead
	animated.play("dead")
	hitbox.process_mode = Node.PROCESS_MODE_DISABLED
	velocity = Vector2.ZERO
	
	
func walk_state(_delta):
	velocity.x = SPEED * direction
	
	if wall_detector.is_colliding():
		scale.x *= -1
		direction *= -1
		
	if not ground_detector.is_colliding():
		scale.x *= -1
		direction *= -1
		
	if player_detector.is_colliding():
		go_to_attack_state()
		return
	
func attack_state(_delta):
	if animated.frame == 2 && can_throw:
		# esse && can_throw faz com que nao saiam diversos ossos por segundo
		throw_bone()
		can_throw = false
	
func dead_state(_delta):
	pass
	
func take_damage():
	go_to_dead_state()
	
func throw_bone():
	var new_bone = SPINNING_BONE.instantiate()
	add_sibling(new_bone)
	new_bone.position = bone_start_position.global_position
	new_bone.set_direction(self.direction)
	
	
func _on_animated_sprite_2d_animation_finished() -> void:
	if animated.animation == "attack":
		go_to_walk_state()
		return
